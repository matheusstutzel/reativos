code/await Player (pool&[] Tiro tiros)
-> FOREVER do

    var float x = outer.scrw/2 - 20/2;
    var float y = outer.scrh-50;
    var float vx = 0;
    var float vy = 0;

    var SDL_Rect r = val SDL_Rect(0,0, 20,50);

    par do
        var int dt;
        every dt in SDL_DT do
            x = x + vx*dt/1000;
            if x<0 then x = outer.scrw; end
            if x>outer.scrw then x = 0; end
        end
    with
        par do
            loop do
                var _SDL_KeyboardEvent&& key;
                key = await SDL_KEYDOWN until key:keysym.sym==_SDLK_LEFT;
                vx = -100;
                key = await SDL_KEYUP   until key:keysym.sym==_SDLK_LEFT;
                vx = 0;
            end
        with
            loop do
                var _SDL_KeyboardEvent&& key;
                key = await SDL_KEYDOWN until key:keysym.sym==_SDLK_UP;
                spawn Tiro((x+7), y ) in tiros;
                await 150ms;
            end
        with
            loop do
                var _SDL_KeyboardEvent&& key;
                key = await SDL_KEYDOWN until key:keysym.sym==_SDLK_RIGHT;
                vx = 100;
                key = await SDL_KEYUP   until key:keysym.sym==_SDLK_RIGHT;
                vx = 0;
            end
        end
    with
        every SDL_REDRAW do
            r.x = x as int;
            r.y = y as int;
			//_SDL_SetRenderDrawColor(&&outer.sdl!.ren, 0x00,0x00,0x00,0xFF);
            //_SDL_RenderFillRect(&&outer.sdl!.ren, (&&r as _SDL_Rect&&));
			_SDL_RenderCopy(&&outer.sdl!.ren, &&outer.imgpl.tex.tex, null, &&r as _SDL_Rect&&);
        end
    end
end
